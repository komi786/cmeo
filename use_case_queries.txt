CQ1:
PREFIX obi:  <http://purl.obolibrary.org/obo/obi.owl/>
PREFIX dc:   <http://purl.org/dc/elements/1.1/>
PREFIX bfo:  <http://purl.obolibrary.org/obo/bfo.owl/>
PREFIX cheo: <https://w3id.org/CHEO/> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ro: <http://purl.obolibrary.org/obo/ro.owl/>

SELECT
    ?study_name
(GROUP_CONCAT(DISTINCT COALESCE(?part_uri); separator=";") AS ?all_parts_uri)
(GROUP_CONCAT(DISTINCT COALESCE(?part_value); separator=";") AS ?all_parts_value)

WHERE {
  GRAPH <https://w3id.org/CHEO/graph/studies_metadata> {
      ?study  a obi:randomized_controlled_trial;
      			ro:has_part   ?protocol;
              dc:identifier ?study_name.
      ?protocol a obi:protocol;
              (ro:has_part)+ ?part_uri .
      ?part_uri cheo:has_value ?part_value.
    
   
   
  }
}
GROUP BY ?study_name

CQ2:
PREFIX obi: <http://purl.obolibrary.org/obo/obi.owl/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX bfo: <http://purl.obolibrary.org/obo/bfo.owl/>
PREFIX cheo: <https://w3id.org/CHEO/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ro: <http://purl.obolibrary.org/obo/ro.owl/>
SELECT DISTINCT ?study_name ?study_type
WHERE { 
    Graph <https://w3id.org/CHEO/graph/studies_metadata> {
        ?study dc:identifier ?study_name;
               rdfs:label ?study_type;
                ro:has_part ?protocol .
    	?protocol a obi:protocol.
    
        ?protocol ro:has_part ?purposeInst .
        ?purposeInst a obi:objective_specification ;
        		cheo:has_value ?purpose .
    	?protocol ro:has_part ?eligibility_criterion.
    	?eligibility_criterion a obi:eligibility_criterion;
              ro:has_part ?inclusion_criteria.
    	?inclusion_criteria a obi:inclusion_criterion;
              ro:has_part  ?specific_inclusion_criteria.
    	?specific_inclusion_criteria  cheo:has_value ?inc_value.
       FILTER(
       contains  (?purpose,"heart failure") ||  contains  (?inc_value,"heart failure") 
       
        )
  }
}

CQ3:

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX obi: <http://purl.obolibrary.org/obo/obi.owl/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX bfo: <http://purl.obolibrary.org/obo/bfo.owl/>
PREFIX cheo: <https://w3id.org/CHEO/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ro: <http://purl.obolibrary.org/obo/ro.owl/>

SELECT ?study_name (GROUP_CONCAT(DISTINCT LCASE(STR(?quality_value)); separator="; ") AS ?qualities)
(GROUP_CONCAT(DISTINCT LCASE(STR(?quality_type)); separator="; ") AS ?qualities_type)
WHERE {
  GRAPH <https://w3id.org/CHEO/graph/studies_metadata> {
    ?study dc:identifier ?study_name ;
           ro:has_part ?protocol .

    ?protocol a obi:protocol ;
              ro:has_part ?eligibility_criterion .

    ?eligibility_criterion a obi:eligibility_criterion ;
                           ro:is_concretized_by ?hse_uri .

    ?hse_uri ro:has_output ?population .
    ?population a obi:population ;
                ro:has_characteristic ?quality .
    OPTIONAL { ?quality cheo:has_value ?quality_value }
    OPTIONAL { ?quality a  ?quality_type }
  }
}
GROUP BY ?study_name
HAVING (
CONTAINS(GROUP_CONCAT(DISTINCT LCASE(STR(?quality_type)); separator="; "), "diabetes") &&
CONTAINS(GROUP_CONCAT(DISTINCT LCASE(STR(?quality_type)); separator="; "), "mixed sex")
)


CQ3.1
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX obi: <http://purl.obolibrary.org/obo/obi.owl/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX bfo: <http://purl.obolibrary.org/obo/bfo.owl/>
PREFIX cheo: <https://w3id.org/CHEO/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ro: <http://purl.obolibrary.org/obo/ro.owl/>

SELECT ?study_name (GROUP_CONCAT(DISTINCT LCASE(STR(?quality_value)); separator="; ") AS ?qualities)
(GROUP_CONCAT(DISTINCT LCASE(STR(?quality_type)); separator="; ") AS ?quality_type)
WHERE {
GRAPH <https://w3id.org/CHEO/graph/studies_metadata> {
?study dc:identifier ?study_name ;
 ro:has_part ?prot .
?prot a obi:protocol ;
 ro:has_part ?ec .
?ec a obi:eligibility_criterion ;
 ro:is_concretized_by ?hse_uri .
?hse_uri ro:has_output ?population .
?population a obi:population ;
 ro:has_characteristic ?quality .
OPTIONAL { ?quality rdfs:label ?quality_value }
OPTIONAL { ?quality a  ?quality_type }
}
}
GROUP BY ?study_name
LIMIT 3


CQ4:
PREFIX cdi: <http://ddialliance.org/Specification/DDI-CDI/1.0/RDF/>
PREFIX cd: <http://citydata.wu.ac.at/ns#>
PREFIX obi: <http://purl.obolibrary.org/obo/obi.owl/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX bfo: <http://purl.obolibrary.org/obo/bfo.owl/>
PREFIX cheo: <https://w3id.org/CHEO/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ro: <http://purl.obolibrary.org/obo/ro.owl/>

SELECT DISTINCT ?study_name 
(GROUP_CONCAT(DISTINCT LCASE(STR(?population_value)); separator="; ") AS ?population_values)
(GROUP_CONCAT(DISTINCT LCASE(STR(?intervention_value)); separator="; ") AS ?intervention_values)
(GROUP_CONCAT(DISTINCT LCASE(STR(?comparator_value)); separator="; ") AS ?comparator_values)
(GROUP_CONCAT(DISTINCT LCASE(STR(?outcome_value)); separator="; ") AS ?outcome_values)
WHERE {  
    Graph <https://w3id.org/CHEO/graph/studies_metadata> {
        ?study dc:identifier ?study_name;
               rdfs:label ?study_type;
                ro:has_part ?protocol .
    	?protocol a obi:protocol.
        # Population from inclusion criteria
   
      ?protocol ro:has_part ?eligibility_criterion .
      ?eligibility_criterion a obi:eligibility_criterion ;
                             ro:has_part ?inclusion_criteria .
      ?inclusion_criteria a obi:inclusion_criterion ;
                          ro:has_part ?inc_part .
      ?inc_part cheo:has_value ?population_value .
      
      FILTER(
       contains  (?population_value,"heart failure")
       
        )
 
     # Intervention
     ?protocol ro:has_part ?intervention .
      ?intervention a cheo:intervention_specification;
                    cheo:has_value ?intervention_value .
     FILTER(
       contains  (?intervention_value,"intensified")
       
        )
         # Comparator
      ?protocol ro:has_part ?comparator .
      ?comparator a cheo:comparator_specification ;
                  cheo:has_value ?comparator_value .
    
    FILTER(
       contains  (?comparator_value,"standard guided")
       
        )
   
    ?protocol ro:has_part ?outcome .
    ?outcome a cheo:outcome_specification ;
                ro:has_part ?suboutcome_uri.
    ?suboutcome_uri  cheo:has_value ?outcome_value .
    
    FILTER(
       contains  (?outcome_value,"survival")
       
        )
   
  }
}
GROUP BY ?study_name


CQ4B:

PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX cdi: <http://ddialliance.org/Specification/DDI-CDI/1.0/RDF/>
PREFIX cd: <http://citydata.wu.ac.at/ns#>
PREFIX obi: <http://purl.obolibrary.org/obo/obi.owl/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX bfo: <http://purl.obolibrary.org/obo/bfo.owl/>
PREFIX cheo: <https://w3id.org/CHEO/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ro: <http://purl.obolibrary.org/obo/ro.owl/>

SELECT DISTINCT ?study_name 
(GROUP_CONCAT(DISTINCT LCASE(STR(?population_value)); separator="; ") AS ?population_values)
(GROUP_CONCAT(DISTINCT LCASE(STR(?intervention_value)); separator="; ") AS ?intervention_values)
(GROUP_CONCAT(DISTINCT LCASE(STR(?comparator_value)); separator="; ") AS ?comparator_values)
(GROUP_CONCAT(DISTINCT LCASE(STR(?outcome_value)); separator="; ") AS ?outcome_values)
WHERE {  
    Graph <https://w3id.org/CHEO/graph/studies_metadata> {
        ?study dc:identifier ?study_name;
               rdfs:label ?study_type;
                ro:has_part ?protocol .
    	?protocol a obi:protocol.
        # Population from inclusion criteria
   
      ?protocol ro:has_part ?eligibility_criterion .
      ?eligibility_criterion a obi:eligibility_criterion ;
                             ro:has_part ?inclusion_criteria .
      ?inclusion_criteria a obi:inclusion_criterion ;
                          ro:has_part ?inc_part1, ?inc_part2 .
      ?inc_part1 a obi:health_status_inclusion_criterion;
      			cheo:has_value ?diagnosis_value .
      FILTER(
       contains  (?diagnosis_value, "new york heart association classification - class ii")
        )
      ?inc_part2 a obi:age_group_inclusion_criterion;
         		ro:has_part ?min_val_specification, ?max_value_specification.
      	
      ?min_val_specification a obi:minimum_age_value_specification;
        		cheo:has_value ?min_val.
      ?max_value_specification a obi:maximum_age_value_specification;
     				cheo:has_value  ?max_value.
 	  FILTER(xsd:integer(?min_val) >= 60 && xsd:integer(?max_value) <= 85)
    
     # Intervention
     ?protocol ro:has_part ?intervention .
      ?intervention a cheo:intervention_specification;
                    cheo:has_value ?intervention_value .
     FILTER(
       contains  (?intervention_value,"intensified")
       
        )
      # Comparator
      ?protocol ro:has_part ?comparator .
      ?comparator a cheo:comparator_specification ;
                  cheo:has_value ?comparator_value .
    FILTER(
       contains  (?comparator_value,"standard guided")
       
        )
    ?protocol ro:has_part ?outcome .
    ?outcome a cheo:outcome_specification ;
                ro:has_part ?suboutcome_uri.
    ?suboutcome_uri  cheo:has_value ?outcome_value .
    FILTER(
       contains  (?outcome_value,"survival")
       
        )
  }
}
GROUP BY ?study_name


CQ5:
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX cdi: <http://ddialliance.org/Specification/DDI-CDI/1.0/RDF/>
PREFIX cd: <http://citydata.wu.ac.at/ns#>
PREFIX obi: <http://purl.obolibrary.org/obo/obi.owl/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX bfo: <http://purl.obolibrary.org/obo/bfo.owl/>
PREFIX cheo: <https://w3id.org/CHEO/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ro: <http://purl.obolibrary.org/obo/ro.owl/>

SELECT DISTINCT ?variable ?missing_value
WHERE {
  GRAPH <https://w3id.org/CHEO/graph/time-chf>  {
    ?dataElement a cheo:data_element;
  				dc:identifier ?variable ;
    			obi:has_value_specification ?missingSpec .
    ?missingSpec a cheo:missing_value_specification;
    		cheo:has_value ?missing_value .
    }
    }
LIMIT 3


CQ6:

PREFIX stato: <http://purl.obolibrary.org/obo/stato.owl/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX cd: <http://citydata.wu.ac.at/ns#>
PREFIX obi: <http://purl.obolibrary.org/obo/obi.owl/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX bfo: <http://purl.obolibrary.org/obo/bfo.owl/>
PREFIX cheo: <https://w3id.org/CHEO/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ro: <http://purl.obolibrary.org/obo/ro.owl/> 
PREFIX iao: <http://purl.obolibrary.org/obo/iao.owl/>


SELECT ?variable ?frequency_distribution
WHERE {
  GRAPH <https://w3id.org/CDIO/graph/time-chf> {
    ?dataElement a cheo:data_element ;
                 dc:identifier ?variable ;
                 obi:is_denoted_by ?statistical_variable .


    ?statistical_variable a ?type .
    VALUES ?type { obi:binary_class_variable obi:multi_class_variable }

    ?dataset a iao:data_set ;
             iao:is_about ?statistical_variable ;
             obi:is_specified_input_of ?eda_proc .

    ?eda_proc obi:has_specified_output ?statistic .
    ?statistic a stato:statistic ;
               ro:has_part ?part_uri .

    ?part_uri a cheo:frequency_distribution ;
              cheo:has_value ?frequency_distribution .
  }
}
Limit 3


CQ7:

 PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd:  <http://www.w3.org/2001/XMLSchema#>
PREFIX dc:   <http://purl.org/dc/elements/1.1/>
PREFIX obi:  <http://purl.obolibrary.org/obo/obi.owl/>
PREFIX cheo: <https://w3id.org/CHEO/>
PREFIX bfo:  <http://purl.obolibrary.org/obo/bfo.owl/>

SELECT
?omop_id ?code_label ?code_value ?val
(GROUP_CONCAT(DISTINCT ?varNameA; SEPARATOR=", ") AS ?source)
(GROUP_CONCAT(DISTINCT ?varNameB; SEPARATOR=", ") AS ?target)
(GROUP_CONCAT(DISTINCT STR(?visitsA); SEPARATOR=", ") AS ?source_visit)
(GROUP_CONCAT(DISTINCT STR(?visitsB); SEPARATOR=", ") AS ?target_visit)

WHERE 
{
    {
        SELECT
        ?omop_id ?code_label ?code_value ?val

        (COUNT(DISTINCT ?primary_code_literal) AS ?codeCountA)
        (GROUP_CONCAT(DISTINCT STR(?var_nameA); SEPARATOR=", ") AS ?varNameA)
        (GROUP_CONCAT(CONCAT(STR(?var_nameA), "||", STR(?visitcodelabelA)); SEPARATOR=", ") AS ?visitsA)
        ("{source_study_name}" AS ?source)
        WHERE {
            GRAPH <https://w3id.org/CHEO/graph/time-chf> 
            {
            ?dataElementA rdf:type cheo:data_element ;
            dc:identifier ?var_nameA ;
            obi:is_specified_input_of ?catProcessA, ?stdProcessA .

            ?visitdatum  rdf:type cheo:visit_measurement_datum ;
            obi:is_about ?dataElementA ;
            obi:is_specified_input_of ?vs_stdProcessA .


            ?vs_stdProcessA obi:has_specified_output ?visit_code.
            ?visit_code rdfs:label ?visitcodelabelA.
        
            ?catProcessA rdf:type cheo:categorization_process ;
            obi:has_specified_output ?cat_outputA .
            ?cat_outputA cheo:has_value ?val .
            #FILTER(?val IN ("measurement", "drug_exposure"))

            ?stdProcessA rdf:type cheo:data_standardization ;
            obi:has_specified_output ?codeA .
            ?codeA rdf:_1 ?primary_code_literal .
            ?primary_code_literal obi:denotes ?omop_id_uri ;
            cheo:has_value ?code_value ;
            rdfs:label ?code_label .
            ?omop_id_uri rdf:type cheo:omop_id ;
            cheo:has_value ?omop_id .
          }
             }
            GROUP BY ?omop_id ?code_label ?code_value ?val
            }
            UNION
            {
            SELECT
            ?omop_id ?code_label  ?code_value ?val
            (COUNT(DISTINCT ?primary_code_literal) AS ?codeCountB)
            (GROUP_CONCAT(DISTINCT STR(?var_nameB); SEPARATOR=", ") AS ?varNameB)
            (GROUP_CONCAT(CONCAT(STR(?var_nameB), "||", STR(?visitcodelabelB)); SEPARATOR=", ") AS ?visitsB)
            ("{target_study_name}" AS ?target)
            WHERE 
            {
            GRAPH <https://w3id.org/CHEO/graph/aachenhf> 
               {
            ?dataElementB rdf:type cheo:data_element ;
            dc:identifier ?var_nameB ;
            obi:is_specified_input_of ?catProcessB, ?stdProcessB.
            ?visitdatum  rdf:type cheo:visit_measurement_datum ;
            obi:is_about ?dataElementB ;
            obi:is_specified_input_of ?vs_stdProcessAB .


            ?vs_stdProcessAB obi:has_specified_output ?visit_code.
            ?visit_code rdfs:label ?visitcodelabelB.
            ?catProcessB rdf:type cheo:categorization_process ;
            obi:has_specified_output ?cat_outputB .
            ?cat_outputB cheo:has_value ?val .
            #FILTER(?val IN ("measurement", "drug_exposure"))

            ?stdProcessB rdf:type cheo:data_standardization ;
            obi:has_specified_output ?codeB .
            ?codeB rdf:_1 ?primary_code_literal .
            ?primary_code_literal obi:denotes ?omop_id_uri ;
            cheo:has_value ?code_value;
            rdfs:label ?code_label.
            ?omop_id_uri rdf:type cheo:omop_id ;
            cheo:has_value ?omop_id.
            }
       }
            GROUP BY ?omop_id  ?code_label ?code_value  ?val
       }
    }
            GROUP BY ?omop_id ?code_label ?code_value ?val
            #HAVING (COUNT(DISTINCT ?source) < 3)
            ORDER BY ?omop_id

